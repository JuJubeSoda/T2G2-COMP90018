plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.google.android.libraries.mapsplatform.secrets.gradle.plugin)
}

android {
    namespace 'com.example.myapplication'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.myapplication"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Map existing MAPS_API_KEY (from secrets/local.defaults) to BuildConfig.PLACES_API_KEY
        def mapsApiKey = ""
        def props = new Properties()
        def secretsFile = rootProject.file("secret.properties")
        def defaultsFile = rootProject.file("local.defaults.properties")
        if (secretsFile.exists()) {
            secretsFile.withInputStream { props.load(it) }
        }
        if (!props.containsKey("MAPS_API_KEY") && defaultsFile.exists()) {
            defaultsFile.withInputStream { props.load(it) }
        }
        mapsApiKey = props.getProperty("MAPS_API_KEY", "")
        buildConfigField "String", "PLACES_API_KEY", '"' + mapsApiKey + '"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    implementation libs.constraintlayout
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.navigation.fragment
    implementation libs.navigation.ui
    implementation libs.play.services.maps
    implementation libs.play.services.location
    implementation libs.places
    implementation libs.camera.view
    implementation libs.camera.lifecycle
    implementation libs.cardview
    implementation "androidx.camera:camera-camera2:1.3.0"
    implementation "com.google.maps.android:android-maps-utils:3.19.0" // Added GeoJSON library

    // Networking
    implementation libs.retrofit2
    implementation libs.retrofit2.converter.gson
    implementation libs.okhttp3
    implementation libs.okhttp3.logging.interceptor

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

secrets {
    // To add your Maps API key to this project:
    // 1. If the secrets.properties file does not exist, create it in the same folder as the local.properties file.
    // 2. Add this line, where YOUR_API_KEY is your API key:
    //        MAPS_API_KEY=YOUR_API_KEY
    propertiesFileName = "secret.properties"

    // A properties file containing default secret values. This file can be
    // checked in version control.
    defaultPropertiesFileName = "local.defaults.properties"
}
